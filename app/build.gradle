buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.3'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 18
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "nz.ac.auckland.lablet"
        minSdkVersion 18
        targetSdkVersion 18
        versionCode 313
        versionName "3.1.2"
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

        release {
            storeFile file(properties.getProperty('localStoreFile'))
            storePassword properties.getProperty('localStorePassword')
            keyAlias properties.getProperty('localKeyAlias')
            keyPassword properties.getProperty('localKeyPassword')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        source = variant.javaCompile.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        classpath = files(variant.javaCompile.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/7/docs/api/"
            linksOffline "http://d.android.com/reference","${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        exclude '**/ScriptC_fft.java'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://raw.github.com/vRallev/mvn-repo/master/'
    }
}

dependencies {
    compile 'org.opencv:opencv-android:2.4.8'
    compile 'au.com.bytecode:opencsv:2.4'
    compile 'edu.emory.mathcs:JTransforms:2.4'
    compile 'org.luaj:luaj-jse:3.0-beta2'
    compile 'com.android.support:support-annotations:20.0.0'
    compile 'com.android.support:support-v4:22.2.0'
    compile fileTree(include: ['*.jar'], dir: 'libs')
}
