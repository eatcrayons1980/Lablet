buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "nz.ac.auckland.lablet"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 319
        versionName "3.1.8"
        renderscriptTargetApi 24
        renderscriptSupportModeEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('keystore.properties').newDataInputStream())

        release {
            storeFile file(properties.getProperty('localStoreFile'))
            storePassword properties.getProperty('localStorePassword')
            keyAlias properties.getProperty('localKeyAlias')
            keyPassword properties.getProperty('localKeyPassword')
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        source = variant.javaCompiler.source
        def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"

        classpath = files(variant.javaCompiler.classpath.files, androidJar)
        options {
            links "http://docs.oracle.com/javase/8/docs/api/"
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
        exclude '**/BuildConfig.java'
        exclude '**/R.java'
        exclude '**/ScriptC_fft.java'
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url 'https://raw.github.com/vRallev/mvn-repo/master/'
    }
    maven {
        url 'http://dl.bintray.com/steveliles/maven'
    }
    google()
}

dependencies {
    // TODO: Remove 'noinspection GradleCompatible' (next line) and fix error
    //noinspection GradleCompatible
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:support-annotations:27.0.0'
    // OpenCV libraries from: https://github.com/steveliles/opencv-android
    compile 'org.opencv:OpenCV-Android:3.1.0'
    compile 'au.com.bytecode:opencsv:2.4'
    compile 'edu.emory.mathcs:JTransforms:2.4'
    compile 'org.luaj:luaj-jse:3.0-beta2'
    compile 'org.jetbrains:annotations:15.0'
    testCompile 'junit:junit:4.12'
}


